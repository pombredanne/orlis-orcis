import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
lintOptions { abortOnError false }
    compileSdkVersion 19
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "org.poirsouille.tinc_gui"
        minSdkVersion 16
        targetSdkVersion 21
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_5
            targetCompatibility JavaVersion.VERSION_1_5
        }

        ndk {
            moduleName "tincd"
        }
    }

    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }

    def ndkDir = android.ndkDirectory
    def nkdBuildCmd = "$ndkDir/ndk-build" + (Os.isFamily(Os.FAMILY_WINDOWS) ? ".cmd" : "")
    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {

        commandLine "$nkdBuildCmd",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        commandLine "$nkdBuildCmd",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    buildTypes {
        release {
minifyEnabled true
proguardFiles '/home/yan/Android/Sdk/tools/proguard/proguard-android.txt'
            zipAlignEnabled true
        }
    }
}

